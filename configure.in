dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/eggdrop.h)
AC_CONFIG_AUX_DIR(.)
AM_CONFIG_HEADER(config.h)
EGG_MSG_CONFIGURE_START
AM_INIT_AUTOMAKE(eggdrop, 1.7.0)

# FIXME: optionally allow a system wide install by bypassing the macro below.
AC_PREFIX_DEFAULT([\${HOME}/eggdrop])

# config.h stuff
AH_TOP([#ifndef _EGG_CONFIG_H
#define _EGG_CONFIG_H])
AH_BOTTOM([#endif /* !_EGG_CONFIG_H */])

# Setup build environment
AC_PROG_CC
EGG_CHECK_CC

# Crazy machines
AC_AIX
AC_ISC_POSIX
AC_MINIX

# Gettext support
EGG_GNU_GETTEXT

# Libtool support
AC_DISABLE_FAST_INSTALL
AC_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AC_LIBLTDL_CONVENIENCE
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

# Speedup compile
EGG_CHECK_CCPIPE

# Checks for programs
AC_PROG_LN_S
EGG_PROG_STRIP
EGG_PROG_AWK
EGG_PROG_BASENAME
AC_CHECK_PROG(EGG_UNAME,uname,uname)
AC_CHECK_PROG(AR,ar,ar)

# Test the os and set the module linking settings
EGG_CHECK_OS

# Checks for system libraries
EGG_CHECK_LIBS

# Checks for header files
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(sys/time.h)
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/select.h sys/rusage.h unistd.h dlfcn.h stdarg.h std_args.h strings.h limits.h)
EGG_INADDR_LOOPBACK

# Checks for typedefs, structures, and compiler characteristics
AC_TYPE_PID_T
AC_TYPE_SIZE_T
EGG_TYPE_SOCKLEN_T
AC_C_CONST
AC_C_BIGENDIAN
AC_C_INLINE
AC_CHECK_SIZEOF(long, 0)
AC_CHECK_SIZEOF(int, 0)

# Checks for library functions
AC_CHECK_FUNCS(clock getrusage setpgid uname sigaction sigemptyset rename strcasecmp strncasecmp getdtablesize random srandom dlopen dprintf snprintf vsnprintf isascii inet_aton strftime fsync inet_pton inet_ntop)
EGG_CHECK_FUNC_VSPRINTF

# Make sure we have stdc headers, since we can't compile without them
EGG_HEADER_STDC

# Checks for cygwin
EGG_CYGWIN

EGG_IPV6_SUPPORTED
EGG_IPV6_OPTIONS

# Where is tcl?  Is it here?
# ---------- begin robey's tcl thingies
# (well, what used to be robey's tcl thingies...)

# Latest tested Tcl version to recommend if Tcl isn't found
tclrecommendver="8.3.3"

# Site recommended to download Tcl from
tclrecommendsite="ftp://ftp.eggheads.org/pub/tcl/tcl8_3/"

# Tcl library filename prefixes (also used for Tcl header dir on FreeBSD)
tcllibnames="tcl tcl8.4 tcl84 tcl8.3 tcl83 tcl8.2 tcl82 tcl8.1 tcl81 \
	tcl8.0 tcl80 tcl7.6 tcl76 tcl7.5 tcl75 tcl7.4 tcl74 \
	tcl7.3 tcl73 tcl7.2 tcl72 tcl7.1 tcl71 tcl7.0 tcl70"

# Tcl library filename suffixes
tcllibextensions=".so .so.1 .so.1.0 .so.1.2 .a .sl .dll"

# Tcl library search paths
tcllibpaths="/usr/local/lib /usr/local/pkgs/tcl/lib \
	/usr/lib /lib /usr/i486-linuxaout/lib \
	/beos/system/lib /sys/lib \
	$HOME/lib $HOME/tcl/lib $HOME"

# Tcl header filenames
tclheadernames="tcl.h"

# Tcl header search paths
tclheaderpaths="/usr/local/include /usr/local/pkgs/tcl/include \
	/usr/include /beos/system/include /beos/devel/include \
       /sys/include $HOME/include $HOME/tcl/include $HOME"

EGG_TCL_WITH_OPTIONS
EGG_TCL_ENV
EGG_TCL_WITH_TCLLIB
EGG_TCL_WITH_TCLINC
EGG_TCL_FIND_LIBRARY
EGG_TCL_FIND_HEADER
EGG_TCL_CHECK_LIBRARY
EGG_TCL_CHECK_HEADER
EGG_TCL_DETECT_CHANGE

EGG_TCL_CHECK_VERSION
EGG_TCL_CHECK_PRE70
EGG_TCL_CHECK_PRE75
EGG_TCL_TESTLIBS
EGG_TCL_CHECK_FREE
EGG_TCL_THREADS_OPTIONS
EGG_TCL_CHECK_THREADS
EGG_TCL_LIB_REQS
EGG_TCL_LUSH
# ---------- end of (what used to be) robey's tcl thingies

EGG_DEBUG_OPTIONS

EGG_DEFINE_VERSION_NUM

EGG_COMPRESS_MODULE

AC_CONFIG_SUBDIRS(libltdl)

AC_SUBST(ac_aux_dir)

# FIXME: module's Makefiles list will prolly become dynamic
AC_OUTPUT([Makefile doc/Makefile scripts/Makefile src/Makefile src/compat/Makefile src/egglib/Makefile src/mod/Makefile src/adns/Makefile src/mod/Makefile intl/Makefile po/Makefile.in src/mod/assoc.mod/Makefile src/mod/blowfish.mod/Makefile src/mod/channels.mod/Makefile src/mod/compress.mod/Makefile src/mod/console.mod/Makefile src/mod/ctcp.mod/Makefile src/mod/filesys.mod/Makefile src/mod/irc.mod/Makefile src/mod/notes.mod/Makefile src/mod/server.mod/Makefile src/mod/share.mod/Makefile src/mod/transfer.mod/Makefile src/mod/uptime.mod/Makefile src/mod/woobie.mod/Makefile])

EGG_MSG_CONFIGURE_END
